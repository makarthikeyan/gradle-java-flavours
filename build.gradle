plugins {
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.10.1'
	id 'groovy'
	id 'maven-publish'
	id "com.gradle.plugin-publish" version '0.11.0'
	id 'com.jfrog.bintray' version '1.8.5'
	id 'java-gradle-plugin'
}

compileJava.options.fork = true
compileJava.options.forkOptions.executable = '/Library/Java/JavaVirtualMachines/jdk1.8.0_251.jdk/Contents/Home/bin/javac'

group = 'com.lazan'
version = file('version.txt').text.trim()

repositories {
	mavenCentral()
}

configurations {
	jacocoRuntime
}

dependencies {
	testImplementation 'junit:junit:4.13'
	testImplementation 'org.mockito:mockito-all:1.10.19'
	testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
	testRuntimeOnly files("$buildDir/testkit")
	jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
	beforeTest { TestDescriptor descriptor ->
		logger.lifecycle("Running test: $descriptor.name")
	}
	dependsOn 'createTestkitFiles'
}

task createTestkitFiles {
	def outputDir = file("$buildDir/testkit")

	inputs.files sourceSets.main.runtimeClasspath
	inputs.files configurations.jacocoRuntime
	outputs.dir outputDir

	doLast {
		outputDir.mkdirs()
		String propString = "org.gradle.jvmargs=-javaagent:${configurations.jacocoRuntime.asPath}=destfile=$buildDir/jacoco/test.exec".replace('\\', '/')
		file("$outputDir/testkit-gradle.properties").text = propString
	}
}

pluginBundle {
	website = 'https://github.com/uklance/gradle-java-flavours'
	vcsUrl = 'https://github.com/uklance/gradle-java-flavours.git'
	description = 'Android style flavours in a java project'
	tags = ['flavor', 'flavour', 'flavors', 'flavours', 'java']

	plugins {
		javaFlavours {
			id = 'com.lazan.javaflavours'
			displayName = 'Java Flavours Plugin'
		}
	}
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

bintray {
	user = project.properties['bintrayUser']
	key = project.properties['bintrayKey']
	publications = ['maven']
	publish = true
	pkg {
		repo = 'maven'
		name = project.name
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/uklance/gradle-java-flavours.git'
		githubRepo = 'uklance/java-flavours'
		labels = ['flavor', 'flavour', 'flavors', 'flavours', 'java', 'gradle', 'plugin']
		version {
			name = project.version
			released = new Date()
			desc = "Gradle Java Flavours Plugin ${project.version}"
		}
	}
}
